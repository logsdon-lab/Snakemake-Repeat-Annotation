include: "rules/common.smk"
include: "rules/split_multifasta.smk"


outputs = []

if config.get("repeatmasker"):

    include: "rules/repeatmasker.smk"

    outputs.append(rules.repeatmasker_all.input)


if config.get("moddotplot"):

    include: "rules/moddotplot.smk"

    outputs.append(rules.moddotplot_all.input)


if config.get("humas_annot"):

    include: "rules/humas_annot.smk"

    outputs.append(rules.humas_annot_all.input)


rule cleanup_split_fa:
    input:
        chkpt=outputs,
        fofn=rules.split_multifasta.output,
    output:
        touch(join(OUTPUT_DIR, "fa", "cleanup_{sm}.done")),
    shell:
        """
        cat {input.fofn} | xargs -I {{}} bash -c "rm {{}}"
        """


rule all:
    input:
        outputs,
        expand(rules.cleanup_split_fa.output, sm=SAMPLES),
    default_target: True
